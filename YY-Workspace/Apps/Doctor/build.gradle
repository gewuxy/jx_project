apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.zhuanyeban.yaya"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk

        versionCode 7110
        versionName "7.1.1"

        multiDexEnabled true

        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                // LibYS里的混淆, 相对路径为3级
                '../../../Android/Libs/LibYS_Rx/proguard-ys.pro',
                '../../../Android/Libs/LibYS_Rx/proguard-fresco.pro',
                '../../../Android/Libs/LibYS_Rx/proguard-lambda.pro',

                '../../../Android/Libs/LibNetwork_Rx/proguard-network.pro',
                '../../../Android/DI-compiler/proguard-injection.pro',
                '../../../Android/Libs/LibBD/proguard-baidu.pro',

                // YaYa-Libs 里的混淆, 相对路径为2级
                '../../Libs/LibJG/proguard-jg.pro',
                '../../Libs/LibPlayer/proguard-player.pro',
                '../../Libs/LibWX/proguard-wx.pro',
                // 本身的混淆
                'proguard-rules.pro'
    }

    signingConfigs {
        release {
            keyAlias 'jx'
            keyPassword '123456'
            storeFile file('./jx.keystore')
            storePassword '123456'
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_LOG", "true"
            buildConfigField "boolean", "DEBUG_NETWORK", "true"
            buildConfigField "boolean", "SWIPE_BACK_ENABLE", "true"
            buildConfigField "boolean", "TEST", "true"

            signingConfig signingConfigs.release

            shrinkResources false
            zipAlignEnabled false
            minifyEnabled false

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
        alpha {
            buildConfigField "boolean", "DEBUG_LOG", "true"
            buildConfigField "boolean", "DEBUG_NETWORK", "true"
            buildConfigField "boolean", "SWIPE_BACK_ENABLE", "false"
            buildConfigField "boolean", "TEST", "false"

            signingConfig signingConfigs.release

            shrinkResources true
            zipAlignEnabled true
            minifyEnabled true

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
        release {
            buildConfigField "boolean", "DEBUG_LOG", "false"
            buildConfigField "boolean", "DEBUG_NETWORK", "false"
            buildConfigField "boolean", "SWIPE_BACK_ENABLE", "false"
            buildConfigField "boolean", "TEST", "false"

            signingConfig signingConfigs.release

            shrinkResources true
            zipAlignEnabled true
            minifyEnabled true

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a"
            }
        }
    }

    productFlavors {
        flavors_debug {
            manifestPlaceholders = [
                    app_name: "YaYa医师(B)",
            ]
        }
        flavors_alpha {
            manifestPlaceholders = [
                    app_name: "YaYa医师(A)",
            ]
        }
        flavors_release {
            manifestPlaceholders = [
                    app_name: "YaYa医师",
            ]
        }
    }

    //在生成的apk文件，修改下命名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 前段
                def fileName = outputFile.name.replace("Doctor", "YaYa医师")
                // 中段
                fileName = fileName.replace("-flavors_debug", "")
                fileName = fileName.replace("-debug", "(test)")
                fileName = fileName.replace("-flavors_alpha", "")
                fileName = fileName.replace("-alpha", "(alpha)")
                fileName = fileName.replace("-flavors_release", "")
                fileName = fileName.replace("-release", "")
                // 尾端
                fileName = fileName.replace(".apk", "_v${defaultConfig.versionName}.apk")

                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    sourceSets {
        main.res.srcDirs += 'src/main/res-form'
    }

    lintOptions {
        abortOnError configs.abortOnError
    }

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

}

dependencies {
    compile project(':YY-Workspace:Libs:LibYY')
    compile project(':YY-Workspace:Libs:LibPay')
    compile project(':YY-Workspace:Libs:LibPlayer')
    compile project(':YY-Workspace:Libs:LibJG')
    compile project(':YY-Workspace:Libs:LibWX')
    compile project(':YY-Workspace:Libs:LibPDF')
    /**
     * other
     */
    compile 'com.mylhyl:zxingscanner:1.2.1'
    compile 'org.greenrobot:greendao:3.2.2'
    /**
     * leakcanary
     */
//    debugCompile deps.debug.leakcanary
//    releaseCompile deps.release.leakcanary

    compile deps.support.dex
}

/**
 * greenDao的配置
 */
apply plugin: 'org.greenrobot.greendao'
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
    }
}
greendao {
    // 数据库schema版本，也可以理解为数据库版本号
    schemaVersion 1000
    //  设置DaoMaster 、DaoSession、Dao包名 默认是实体的包名
//    daoPackage  '***'
    //  设置DaoMaster 、DaoSession、Dao目录
//    targetGenDir 'src/main/java/lib/yt/db'
    //  设置生成单元测试目录
//    targetGenDirTest
//    //  设置自动生成单元测试用例
//    generateTests
}

